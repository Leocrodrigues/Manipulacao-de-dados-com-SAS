/* Declaração da biblioteca da AluraPlay*/
LIBNAME alura '/home/u59759055/AluraPlay';


/* ANÁLISE DE QUANTOS JOGOS CLIENTES 
 *ALUGOU  NO QUARTO TRIMESTRE DE 2017*/

/*ANÁLISE DAS BASES DE OPERAÇÕES*/

PROC CONTENTS data=alura.operacoes_201710 VARNUM;RUN;
PROC CONTENTS data=alura.operacoes_201711 VARNUM;RUN;
PROC CONTENTS data=alura.operacoes_201712 VARNUM;RUN;
		
/*EMPILHAR AS BASES DO QUARTO TRIMESTRE DE 2017*/
PROC SQL;
	CREATE TABLE OPERACOES_2017T4 AS
	SELECT *
	FROM alura.operacoes_201710
	UNION ALL /*USANDO ALL PARA NAO DESCONSIDERAR VALORES IGUAIS*/
	SELECT *
	FROM alura.operacoes_201711
	UNION ALL
	SELECT *
	FROM alura.operacoes_201712
;QUIT;

/*FAZENDO COM DATASTEP*/
DATA OPERACOES_2017T4;
SET alura.operacoes_201710
	alura.operacoes_201711
	alura.operacoes_201712;
RUN;


/*GERA BASE COM RESULTADO CONSOLIDADO DO QUARTO TRIMESTRE DE 2017 */
PROC SQL;
	CREATE TABLE cadastro_cliente_jogos_2017T4 AS
	SELECT A.*, B.TOTAL_CONTRATOS_2017T4, B.CONTRATOS_VALIDOS_2017T4
	FROM alura.cadastro_cliente_v3 AS A
	LEFT JOIN ( 
		SELECT CPF,
			COUNT(*) AS TOTAL_CONTRATOS_2017T4, 
			SUM(CASE WHEN DATA_RETORNO - DATA_RETIRADA > 30 OR CUSTO_REPARO > 0
				THEN 0
				ELSE 1
				END) AS CONTRATOS_VALIDOS_2017T4
	FROM operacoes_2017T4
	GROUP BY CPF) AS B
	ON input(substr(A.CPF,1,11),COMMAX11.0) = B.CPF
;QUIT;


/*ANALISAR A QUANTIDADE DE JOGOS DOS CLIENTES EM CADA UM DOS MESES DO QUARTO TRIMESTRE 2017	*/

PROC SQL;
	CREATE TABLE OPERACOES_2017T4_CONSOLIDADO AS
	SELECT SAFRA, CPF, 
		COUNT(*) AS TOTAL_CONTRATOS_2017T4, 
		SUM(CASE WHEN DATA_RETORNO - DATA_RETIRADA > 30 OR CUSTO_REPARO > 0
				THEN 0
				ELSE 1
				END) AS CONTRATOS_VALIDOS_2017T4
	FROM operacoes_2017T4
	GROUP BY 1,2
;QUIT;

/*SEPARAR CADA MÊS DA BASE DE OPERAÇÕES CONSOLIDADA*/
DATA OPERACOES_201710_CONSOLIDADA
	OPERACOES_201711_CONSOLIDADA
	OPERACOES_201712_CONSOLIDADA;
SET OPERACOES_2017T4_CONSOLIDADO;

IF SAFRA = 201710 THEN OUTPUT OPERACOES_201710_CONSOLIDADA;ELSE
IF SAFRA = 201711 THEN OUTPUT OPERACOES_201711_CONSOLIDADA;ELSE
IF SAFRA = 201712 THEN OUTPUT OPERACOES_201712_CONSOLIDADA;

RUN;

/*cruzamento da base clientes com as operações de 2017 USANDO MACRO*/

%LET SAFRA =201710; /*MACRO VARIAVEL*/

%MACRO CRUZA_CLIENTES_JOGOS (SAFRA);	/*CRIANDO MACRO FUNÇÃO*/

PROC SQL;
	CREATE TABLE CADASTRO_CLIENTE_JOGOS_&SAFRA AS
	SELECT  A.*,
			B.TOTAL_CONTRATOS_2017T4 AS TOTAL_CONTRATOS_&SAFRA,
			B.CONTRATOS_VALIDOS_2017T4 AS CONTRATOS_VALIDOS_&SAFRA
	FROM alura.cadastro_cliente_v3 as A
	LEFT JOIN OPERACOES_&SAFRA._CONSOLIDADA as B
	ON input(substr(A.CPF,1,11),COMMAX11.0) = B.CPF
;QUIT;

%MEND CRUZA_CLIENTES_JOGOS; /*ENCERRRANDO A FUNÇÃO DA MACRO*/

/*CHAMANDO A MACRO*/
%CRUZA_CLIENTES_JOGOS(201710);
%CRUZA_CLIENTES_JOGOS(201711);
%CRUZA_CLIENTES_JOGOS(201712);